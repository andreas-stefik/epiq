use Libraries.Game.Game
use Libraries.Game.Scenes.Scene
use Libraries.System.File
use Libraries.Interface.Item3D
use Libraries.Interface.Events.KeyboardListener
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Compute.Vector3
use Libraries.Compute.Math
use Libraries.Sound.Audio
use Libraries.Interface.Events.CollisionListener3D
use Libraries.Interface.Events.CollisionEvent3D
use Libraries.Containers.Array

/*
    This is a default class for creating a game in Quorum. Note that the Main 
    action begins by calling StartGame(), which loads a window a game can 
    be displayed in. The action CreateGame is where the game should load any 
    assets, like images to be displayed, frames of animation, or sounds to be
    played. 
*/
class Main is Game, KeyboardListener, CollisionListener3D
    /* Notice we moved these definitions up here. These are called "fields" 
       and they are available to be used anywhere in our program.
    */
    Item3D player = undefined
    Item3D npc = undefined

    //We now need to move the potions up here as well
    Array<Item3D> goodItems

    //Our background music
    Audio music
    Audio potion

    boolean hasTriggeredQuest = false
    /*
        This action, Main, starts our computer program. In Quorum, programs always
        begin from Main.
    */
    action Main
        StartGame()
    end

    /*
        Inside of this template, we load up a scene from on disk. 
        The game automatically knows and can read these files.
    */
    action CreateGame
        File file
        file:SetPath("Scenes/Scene.qs")
        LoadScene(file)
        
        //Get the items from the scene, like the last tutorial
        //But now we put them in fields up above.
        player = FindItem3DByName("Player")
        npc = FindItem3DByName("NPC")

        //Optional: We can make our character not rotate and slow down
        //with some extra physics calls
        player:SetLinearDamping(0.1)
        player:CanRotate(false)

        //Get all of the potions that we added
        //For this version, we move them to fields
        goodItems:Add(FindItem3DByName("Blue potion 1"))
        goodItems:Add(FindItem3DByName("Blue potion 2"))
        goodItems:Add(FindItem3DByName("Blue potion 3"))
        goodItems:Add(FindItem3DByName("Blue potion 4"))
        goodItems:Add(FindItem3DByName("Blue potion 5"))

        //potions do not have physics enabled, so 
        //we need to tell the system manually to let them collide
        //we could also turn on physics and make them unmovable in the
        //scene editor
        i = 0
        repeat while i < goodItems:GetSize()
            Item3D item = goodItems:Get(i)
            //now we can do both of these operations easily
            //no matter how many good items we add
            item:SetCollidable(true)
            item:Hide()
            i = i + 1
        end 

        //load the background music
        music:LoadToStream("Resources/Sounds/Funshine.ogg")
        music:SetVolume(0.05)
        music:Play()

        //Credit for the sound available at freesound
        //https://freesound.org/people/Jamius/sounds/41529/
        potion:Load("Resources/Sounds/Potion.ogg")

        //Register a keyboard listener to get messages when the user types
        AddKeyboardListener(me)
        
        //Register a listener for the collision system
        AddCollisionListener(me)

        //We need to tell our game to turn on physics in general.
        EnablePhysics3D(true)
        SetGravity3D(0,-10,0)
    end

    /*
        The update action is called on every "frame" of animation in a game. This
        action should be used to do operations on items. So, for example, we 
        might use this action to change frames of animation, to rotate items, 
        or to make creatures jump.
    */
    action Update(number seconds)
        music:Stream()
    end

    /*  When a key is pressed, this code executes.
        Press CTRL + J (PC) or CMD + J on the word "KeyboardEvent" to jump to 
        that location and see what code lives there. This is helpful to learn all of
        the commands. Note that memorizing them is not the norm. 
     */
    action PressedKey(KeyboardEvent event)
        integer code = event:keyCode
        xPlayer = player:GetX()
        zPlayer = player:GetZ()
        Math math

        //Use a vector, like in math, to tell our system
        //how much force to apply to our player in a direction
        Vector3 v
        //This is the number of newtons (a unit of force) to apply
        number amount = 200

        //Use an if statement to apply force for various keys.
        if code = event:UP
            v:SetZ(amount)
        elseif code = event:DOWN
            v:SetZ(-amount)
        elseif code = event:LEFT
            v:SetX(-amount)
        elseif code = event:RIGHT
            v:SetX(amount)
        elseif code = event:SPACE
            v:SetY(amount)
        elseif code = event:F and hasTriggeredQuest //only do this if the quest is triggered
            //set the current smallest distance to the largest possible number
            //then create a placeholder in memory for an Item2D
            number smallest = amount:GetMaximumValue()
            Item3D closest = undefined

            //loop through all of the items and find which one is the closest
            i = 0
            repeat while i < goodItems:GetSize()
                Item3D item = goodItems:Get(i)
                number itemX = item:GetX()
                number itemZ = item:GetZ()

                //this is the same equation from before
                number xd = (xPlayer - itemX) * (xPlayer - itemX)
                number zd = (zPlayer - itemZ) * (zPlayer - itemZ)
                number distance = math:SquareRoot(xd + zd)
                
                //if it's smaller, maybe it's that one
                if distance < smallest
                    smallest = distance
                    closest = item
                end
                i = i + 1
            end

            //did we find one? If so, give its name and distance
            if closest not= undefined
                output closest:GetName() + " Distance " + smallest
            end
        elseif code = event:Q
            xNPC = npc:GetX()
            zNPC = npc:GetZ()
            
            //I wrote the distance formula manually, but do this how you wish
            //It is Square Root((x1 - x2)^2 + (y1 - y2)^2)
            number xd = (xPlayer - xNPC) * (xPlayer - xNPC)
            number zd = (zPlayer - zNPC) * (zPlayer - zNPC)
            number distance = math:SquareRoot(xd + zd)

            number maxDistance = 100
            if distance < maxDistance and not hasTriggeredQuest
                output "Help, help! Evil Lord Bunnykins is rampaging across the countryside. 
If only I had 5 blue potions, I might be able to create an uber potion to strengthen us."

                //We now use a loop for all potion operations
                i = 0
                repeat while i < goodItems:GetSize()
                    Item3D item = goodItems:Get(i)
                    item:Show()
                    i = i + 1
                end
                hasTriggeredQuest = true
            elseif distance < maxDistance and hasTriggeredQuest
                output "I've already told you about the potions. Please help me."
            end
        end

        //Use the force
        player:ApplyForceToCenter(v)
    end

    /* When a key is released, this code executes */
    action ReleasedKey(KeyboardEvent event)
    end

    /*
        This tells us what items are colliding. 
    */
    action BeginCollision(CollisionEvent3D event)
        Item3D a = event:GetItemA()
        Item3D b = event:GetItemB()

        //remove the potions and play a sound
        //if a is a potion and b is the player
        text name = a:GetName()
        boolean isPlayer = b:GetName() = "Player"
        i = 0
        repeat while i < goodItems:GetSize()
            Item3D item = goodItems:Get(i)
            if name = item:GetName() and isPlayer
                potion:Play()
                Remove(a)
            end
            i = i + 1
        end

        //if b is the potion and a is the player
        name = b:GetName()
        isPlayer = a:GetName() = "Player"
        i = 0
        repeat while i < goodItems:GetSize()
            Item3D item = goodItems:Get(i)
            if name = item:GetName() and isPlayer
                potion:Play()
                Remove(b)
            end
            i = i + 1
        end
    end
end